/**
 * Build commands for testing against the latest BRJS develop branch
 */

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.9.0'
    }
}

import org.ajoberstar.grgit.*
import org.ajoberstar.grgit.operation.ResetOp;

task cloneBrjs, type:Exec, {
    commandLine 'git', 'clone', 'https://github.com/BladeRunnerJS/brjs.git', "${buildDir}/brjs-source"
}
cloneBrjs.onlyIf {
    !file("${buildDir}/brjs-source").exists()
}

task fetchBrjs, type:Exec, dependsOn: cloneBrjs, {
    workingDir file("${buildDir}/brjs-source")
    commandLine 'git', 'fetch'
}

task brjsSubmoduleDeInit, type:Exec, dependsOn: cloneBrjs, {
    workingDir file("${buildDir}/brjs-source")
    commandLine 'git', 'submodule', 'deinit', '.'
}

def getCheckoutVersion = {
    if(project.hasProperty("brjsVersion")) {
        return project.property("brjsVersion")
    } else {
        return "master"
    }
}

task checkoutBrjsVersion, type:Exec, dependsOn: [fetchBrjs, brjsSubmoduleDeInit], {
    workingDir file("${buildDir}/brjs-source")
    commandLine 'git', 'checkout', '-f', getCheckoutVersion()
}

task brjsSubmoduleInit, type:Exec, dependsOn: checkoutBrjsVersion, {
    workingDir file("${buildDir}/brjs-source")
    commandLine 'git', 'submodule', 'init'
}

task brjsSubmoduleSync, type:Exec, dependsOn: brjsSubmoduleInit, {
    workingDir file("${buildDir}/brjs-source")
    commandLine 'git', 'submodule', 'sync'
}

task brjsSubmoduleUpdate, type:Exec, dependsOn: brjsSubmoduleSync, {
    workingDir file("${buildDir}/brjs-source")
    commandLine 'git', 'submodule', 'update'
}

task buildBrjs, type:Exec, dependsOn: brjsSubmoduleUpdate, {
    workingDir file("${buildDir}/brjs-source/brjs-sdk")
    def extension = ""
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        extension = ".bat"
    }
    def gradlew = new File(workingDir, "../gradlew" + extension)
    commandLine gradlew.getCanonicalPath(), 'assemble'
}

task getBrjs, dependsOn: buildBrjs, {
    project.brjsPath = file("${buildDir}/brjs-source/brjs-sdk/workspace").getCanonicalPath()
}
